plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'org.beryx.jlink' version '3.0.1' // Check for the latest version
}

group 'at.hsol'
version '1.0'

repositories {
    mavenCentral()
}

// If you're using application plugin
application {
    mainClassName = 'at.hsol.fountainizer.ui.simplegui.simpleGuiv1.Fountainizer'
    mainModule = 'fountainizer.gui.simplegui'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'org.openjfx:javafx-controls:19.0.2.1'
    implementation 'org.openjfx:javafx-fxml:19.0.2.1'
    implementation project(':fountainizer-parser-fountain')
    implementation project(':fountainizer-printer-pdf')
    implementation project(':fountainizer-printer-html')
    implementation project(':fountainizer-api')
}

javafx {
    version = "19.0.2.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

test {
    useJUnitPlatform()
}

ext.os = org.gradle.internal.os.OperatingSystem.current()

jlink {
    imageZip = file("$buildDir/image-zip/fountainizer-0.1b.zip")
    options = ['--bind-services', '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'Fountainizer'
        noConsole = true
    }
    jpackage {
        if(os.windows) {
            installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu']
        }
    }
}